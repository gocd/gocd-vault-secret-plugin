/*
 * Copyright 2019 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"
apply plugin: 'java'

gocdPlugin {
  id = 'com.thoughtworks.gocd.secretmanager.vault'
  pluginVersion = '1.2.1'
  goCdVersion = '20.9.0'
  name = 'Vault secret manager plugin'
  description = 'The plugin allows to use hashicorp vault as secret manager for the GoCD server'
  vendorName = 'ThoughtWorks, Inc.'
  vendorUrl = 'https://github.com/gocd-private/gocd-vault-secret-plugin'

  githubRepo {
    owner = System.getenv('GITHUB_USER') ?: 'bob'
    repo = 'gocd-vault-secret-plugin'
    token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
  }

  pluginProject = project

  prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
  assetsToRelease = [project.tasks.findByName('jar')]
}

sourceCompatibility = 1.11
targetCompatibility = 1.11

version = gocdPlugin.fullVersion(project)

repositories {
  mavenCentral()
}

ext {
  deps = [
    gocdPluginApi: 'cd.go.plugin:go-plugin-api:22.2.0',
    mockito      : 'org.mockito:mockito-core:4.8.1',
  ]

  versions = project.ext.deps.collectEntries { lib, libGav -> [lib, libGav.split(':').last()] }
}

dependencies {
  compileOnly project.deps.gocdPluginApi
  implementation group: 'cd.go.plugin.base', name: 'gocd-plugin-base', version: '0.0.2'
  implementation group: 'com.bettercloud', name: 'vault-java-driver', version: '5.1.0'
  implementation group: 'com.google.code.gson', name: 'gson', version: '2.10'

  testImplementation platform('org.junit:junit-bom:5.9.1')
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params'
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'

  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.23.1'
  testImplementation project.deps.mockito
  testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: project.versions.mockito
  testImplementation group: 'org.jsoup', name: 'jsoup', version: '1.15.3'
  testImplementation project.deps.gocdPluginApi
  testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.1'
}

test {
  useJUnitPlatform()
}

jar {
  from(configurations.runtimeClasspath) {
    into "lib/"
  }
}

